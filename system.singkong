#
    system.singkong
    System

    Bakwan: simple accounting software
    written in Singkong Programming Language

    https://github.com/nopri/bakwan  
    Started by: Noprianto <nop@noprianto.com>
;

load_module("db_util")

var system_db_table = "bakwan"
var system_db_val = {
    "param": "version",
    "value_text": "1"
}
var system_db_user_group = "user_group"
var system_db_user_account = "user_account"

var system_db_latest = 5

var system_default_user = "admin"
var system_default_password = "admin"

var system_db_type_user = "User"
var system_db_type_embedded = "Embedded"
var system_db_type_network = "Network"
var system_db_type_pg = "PostgreSQL"
var system_db_types = [
    system_db_type_user, 
    system_db_type_embedded,
    system_db_type_network,
    system_db_type_pg
]
var system_db_types_ = join(",", system_db_types)
var system_db_types_derby = [
    system_db_type_user, 
    system_db_type_embedded,
    system_db_type_network
]
var system_db_types_pg = system_db_type_pg

var system_auth_len = 4

var system_get_string = fn(o) {
    if (o == null) {
        return ""
    }
    return string(o)
}

var system_connect_db = fn(dt, dh, du, dp, dn) {
    var n = ctx["name"]
    if (n == null) {
        return n
    }

    var c = "//" + dh + "/" + dn
    if (dt == system_db_type_user) {
        return db_connect_embed_user(n)
    } 
    if (dt == system_db_type_embedded) {
        return db_connect_embed(dn)
    }
    if (dt == system_db_type_network) {
        return db_connect("derby", c, du, dp)
    }
    if (dt == system_db_type_pg) {
        return db_connect("postgresql", c, du, dp)
    }
    return null
}

var system_read_config = fn() {
    return properties_read(ctx["conf_path"])
}

var system_get_db_info = fn(p) {
    var conf = system_read_config()
    var ret = [
        _("label_db_type") + ": " + conf["db_type"],
        _("label_db_host") + ": " + conf["db_host"],
        _("label_db_user") + ": " + conf["db_user"],
        _("label_db_name") + ": " + conf["db_name"]
    ]
    if (p == true) {
        ret + (_("label_db_password") + ": " + conf["db_password"])
    }
    return ret
}

var system_get_db = fn() {
    var conf = system_read_config()
    var db_host = conf["db_host"]
    var db_user = conf["db_user"]
    var db_password = conf["db_password"]
    var db_name = conf["db_name"]
    var db_type = conf["db_type"]
    return system_connect_db( 
        db_type, db_host, db_user, db_password,
        db_name)
}

var system_is_new = fn() {
    var db = ctx["db"]
    if (db == null) {
        return null
    }

    var r = db_select_all(db, system_db_table)
    return r == null
}

var system_init_db = fn() {
    var db = ctx["db"]
    if (db == null) {
        return null
    }
    var dt = ctx["db_type"]

    var system_col = [
        ["bakwan_id", "id."],
        ["param", "varchar"],
        ["value_text", "text."]
    ]
    var user_group_col = [
        ["user_group_id", "id."],
        ["name", "varchar"]
    ]
    var user_group_val = {
        "name": "default"
    }
    var user_account_col = [
        ["user_account_id", "id."],
        ["user_account_group_id", "integer"],
        ["name", "varchar"],
        ["salt", "varchar"],
        ["password", "varchar"]
    ]
    var user_account_salt = random_string(16, 16)
    var user_account_val = {
        "name": "default",
        "user_account_group_id": null,
        "name": system_default_user,
        "salt": user_account_salt,
        "password": md5(
            system_default_password + user_account_salt
        )
    }

    if (in(system_db_types_derby, dt)) {
        var r = db_create_table_derby(db, system_db_table, system_col)
        var r = db_insert(db, system_db_table, system_db_val)
        var r = db_create_table_derby(db, system_db_user_group, 
            user_group_col)
        var r = db_insert(db, system_db_user_group, user_group_val)
        var gid = db_last_derby(db)
        if (gid == null) {
            return null
        }
        var r = db_create_table_derby(db, system_db_user_account, 
            user_account_col)
        set(user_account_val, "user_account_group_id", gid)
        var r = db_insert(db, system_db_user_account, user_account_val)

    }
    if (dt == system_db_types_pg) {
        var r = db_create_table_postgresql(db, system_db_table, system_col)
        var r = db_insert(db, system_db_table, system_db_val)
        var r = db_create_table_postgresql(db, 
            system_db_user_group, user_group_col)
        var r = db_insert(db, system_db_user_group, user_group_val)
        var gid = db_last_postgresql(db)
        if (gid == null) {
            return null
        }
        var r = db_create_table_postgresql(db, system_db_user_account, 
            user_account_col)
        set(user_account_val, "user_account_group_id", gid)
        var r = db_insert(db, system_db_user_account, user_account_val)
    }
    return true 
}

var system_db_get_version = fn() {
    var db = ctx["db"]
    if (db == null) {
        return null
    }

    var r = db_query_single(db, 
        "select value_text from bakwan where param=?",
        ["version"]
    )
    if (r == null) {
        return null
    }

    var r = r[0]
    if (len(r) != 1) {
        return null
    }
    
    var v = number(r[0][0])
    if (v < 1) {
        return null
    }

    return v
}

var system_db_set_version = fn(v) {
    var db = ctx["db"]
    if (db == null) {
        return null
    }

    var r = db_query_single(db, 
        "update bakwan
        set value_text=?
        where param=?",
        [string(v), "version"]
    )
    if (r == null) {
        return null
    }
    return true
}

var system_upgrade_db = fn() {
    var db = ctx["db"]
    if (db == null) {
        return null
    }
    var dt = ctx["db_type"]

    var res = []
    var r = repeat {
        var v = system_db_get_version()
        if (v == system_db_latest) {
            return false
        }
        if (v == null) {
            return null
        }
        if (v == 1) {
            var sql_v1 = "alter table user_account 
                add constraint fk_user_account_group_id 
                foreign key (user_account_group_id)
                references user_group(user_group_id)
                "
            var r = db_query_simple(db, sql_v1)
            if (r == null) {
                return null
            }
        }

        if (v == 2) {
            var account_coa_col = [
                ["account_coa_id", "id."],
                ["account_type", "integer"],
                ["code", "varchar"],
                ["description", "varchar"]
            ]

            if (in(system_db_types_derby, dt)) {
                var r = db_create_table_derby(db, "account_coa", account_coa_col)
                if (r[0] == null) {
                    return null                
                }
            }
            if (dt == system_db_types_pg) {
                var r = db_create_table_postgresql(db, "account_coa", account_coa_col)
                if (r[0] == null) {
                    return null                
                }
            }
        }

        if (v == 3) {
            var account_transaction_col = [
                ["account_transaction_id", "id."],
                ["transaction_type", "varchar."],
                ["description", "varchar"],
                ["event", "timestamp"]
            ]

            var account_journal_col = [
                ["account_journal_id", "id."],
                ["account_journal_transaction_id", "integer"],
                ["account_journal_coa_id", "integer"],
                ["amount", "decimal."],
                ["debit", "decimal."],
                ["credit", "decimal."],
                ["reference", "varchar"]
            ]

            if (in(system_db_types_derby, dt)) {
                var r = db_create_table_derby(db, "account_transaction", account_transaction_col)
                if (r[0] == null) {
                    return null                
                }
                var r = db_create_table_derby(db, "account_journal", account_journal_col)
                if (r[0] == null) {
                    return null                
                }
            }
            if (dt == system_db_types_pg) {
                var r = db_create_table_postgresql(db, "account_transaction", account_transaction_col)
                if (r[0] == null) {
                    return null                
                }
                var r = db_create_table_postgresql(db, "account_journal", account_journal_col)
                if (r[0] == null) {
                    return null                
                }
            }

            var sql_v3_1 = "alter table account_journal 
                add constraint fk_account_journal_transaction_id 
                foreign key (account_journal_transaction_id)
                references account_transaction(account_transaction_id)
                "
            var r = db_query_simple(db, sql_v3_1)
            if (r == null) {
                return null
            }
            var sql_v3_2 = "alter table account_journal 
                add constraint fk_account_journal_coa_id 
                foreign key (account_journal_coa_id)
                references account_coa(account_coa_id)
                "
            var r = db_query_simple(db, sql_v3_2)
            if (r == null) {
                return null
            }
        }

        if (v == 4) {
            var a = db_select_all(db, "account_coa")
            if (a == null) {
                return null
            }
            if (empty(a[0])) {
                each(account_template, fn(e, i) {
                    var d = {
                        "account_type": account_types_code[e[0]],
                        "code": e[1],
                        "description": e[2]
                    }
                    db_insert(db, "account_coa", d)
                })
            }
            var a = db_select_all(db, "account_coa")
            if (a == null) {
                return null
            }
            if (empty(a[0])) {
                return null
            }
        }

        var cv = v + 1
        var rv = system_db_set_version(cv)
        if (rv == null) {
            return null
        }
        if (rv == true) {
            res + rv
        }
        if (cv == system_db_latest) { 
            return true
        }
    }

    if (r == null) {
        return null
    }
    if (is_array_of(res, "BOOLEAN")) {
        return true
    }
    return false
}

var system_auth = fn(u, p) {
    var db = ctx["db"]
    if (db == null) {
        return null
    }
    var r = db_query_single(db, 
        "select salt from user_account where name=?", 
        [u]
    )
    if (r == null) {
        return null
    }

    var r = r[0]
    if (len(r) == 1) {
        var s = r[0][0]
        var h = md5(p + s)
        var rr = db_query_single(db, 
            "select user_account_id, 
                user_account_group_id,
                name
            from user_account
                where
            name=? and password=?",
            [u, h]
        )
        if (rr != null) {
            var rr = rr[0]
            if (len(rr) == 1) {
                var a = rr[0]
                var b = {}
                return [a[0], a[1], a[2], b]
            }
        }
    }
    return []
}

var system_privileges_template = fn() {
    return {
        _select: false,
        _insert: false,
        _update: false,
        _delete: false
    }
}

