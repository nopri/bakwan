#
    config.singkong
    Database configuration

    Bakwan: simple accounting software
    written in Singkong Programming Language

    https://github.com/nopri/bakwan  
    Started by: Noprianto <nop@noprianto.com>
;

load_module("db_util")

var config_set_db_conn_param = fn(t, h, u, p, n) {
    var dt = get(t, "text")
    ui_c_enable_text(h)
    ui_c_enable_text(u)
    ui_c_enable_text(p)
    ui_c_enable_text(n)

    if (dt == "User") {
        ui_c_disable_text(h)
        ui_c_disable_text(u)
        ui_c_disable_text(p)
        ui_c_disable_text(n)
        return dt
    } 
    if (dt == "Embedded") {
        ui_c_disable_text(h)
        ui_c_disable_text(u)
        ui_c_disable_text(p)
        config(n, "focus", true) 
        return dt
    }
    config(h, "focus", true) 
    return dt
}

var config_test_db_conn = fn(t, h, u, p, n) {
    var dt = trim(get(t, "text"))
    var dh = trim(get(h, "contents"))
    var du = trim(get(u, "contents"))
    var dp = get(p, "contents")
    var dn = trim(get(n, "contents"))

    return system_connect_db(dt, dh, du, dp, dn)
}

var config_form = fn() {
    var ret = {}
    var r = []

    var g = component("grid", "")

    var btn_save = ui_c_btn_(_("cmd_save"), 0, _m("cmd_save"))
    var btn_connect = ui_c_btn_(_("cmd_test_connect"), 1, _m("cmd_test_connect"))
    var btn_quit = ui_c_btn_(_("cmd_quit"), 5, _m("cmd_quit"))

    var g_btn = component("grid", "")
    grid_add(g_btn, btn_connect, 0, 0, 1, 1, 1, 0, 0, 1)
    grid_add(g_btn, btn_quit, 1, 0, 1, 1, 1, 0, 0, 2)
    grid_add(g_btn, btn_save, 2, 0, 1, 1, 0, 0, 0, 2)

    var lbl_type = component("label", _("label_db_type"))
    var cmb_type = component("combobox", system_db_types_)
    var lbl_host = component("label", _("label_db_host"))
    var txt_host = component("text", "")
    var lbl_user = component("label", _("label_db_user"))
    var txt_user = component("text", "")
    var lbl_pass = component("label", _("label_db_password"))
    var pas_db = component("password", "")
    var lbl_name = component("label", _("label_db_name"))
    var txt_name = component("text", "")

    var gp = component("grid", "")

    grid_add(g, lbl_type, 0, 0, 1, 1, 0, 0, 0, 1)
    grid_add(g, cmb_type, 1, 0, 2, 1, 1, 0, 1, 1)
    grid_add(g, lbl_host, 0, 1, 1, 1, 0, 0, 0, 1)
    grid_add(g, txt_host, 1, 1, 2, 1, 1, 0, 1, 1)
    grid_add(g, lbl_user, 0, 2, 1, 1, 0, 0, 0, 1)
    grid_add(g, txt_user, 1, 2, 2, 1, 1, 0, 1, 1)
    grid_add(g, lbl_pass, 0, 3, 1, 1, 0, 0, 0, 1)
    grid_add(g, pas_db, 1, 3, 2, 1, 1, 0, 1, 1)
    grid_add(g, lbl_name, 0, 4, 1, 1, 0, 0, 0, 1)
    grid_add(g, txt_name, 1, 4, 2, 1, 1, 0, 1, 1)
    grid_add(g, gp, 0, 5, 2, 1, 1, 1, 3, 0)
    grid_add(g, g_btn, 0, 6, 3, 1, 1, 0, 3, 0)

    config_set_db_conn_param(cmb_type, txt_host, txt_user, pas_db, txt_name)
    event(cmb_type, fn() {
        config_set_db_conn_param(cmb_type, 
            txt_host, 
            txt_user, 
            pas_db, 
            txt_name
        )
    })

    event(btn_connect, fn() {
        var db = config_test_db_conn(cmb_type, txt_host, txt_user, pas_db, txt_name)
        if (db == null) {
            message(_("error_db_connect"), _("title_error"))
        } else {
            message(_("success_db_connected"), _("title_info"))
        }
    })

    event(btn_save, fn() {
        set(ret, "command", "save")
        set(ret, "config", false) 

        var db = config_test_db_conn(cmb_type, txt_host, txt_user, pas_db, txt_name)
        if (db == null) {
            message(_("error_db_connect"), _("title_error"))
        } else{
            var db_type_input = trim(get(cmb_type, "text"))
            var host_input = trim(get(txt_host, "contents"))
            var user_input = trim(get(txt_user, "contents"))
            var password_input = get(pas_db, "contents")
            var name_input = trim(get(txt_name, "contents"))
            
            var config_hash = {
                "db_type": db_type_input,
                "db_host": host_input,
                "db_user": user_input,
                "db_password": password_input,
                "db_name": name_input
            }

            if(properties_write(ctx["conf_path"], config_hash)){
                message(_("success_config_write"), _("title_info"))
                set(ret, "config", true)
                custom_dialog_close(last(r))
            }else{
                message(_("error_config_write"), _("title_error"))
            }
        }
    })

    event(btn_quit, fn() {
        set(ret, "command", "close")
        custom_dialog_close(last(r))
    })

    custom_dialog(g, _("title_db_config"), 
        ctx["dialog_width"], ctx["dialog_height"], 
        true, r)

    return ret
}

