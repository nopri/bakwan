#
    ui.singkong
    User interface utilities

    Bakwan: simple accounting software
    written in Singkong Programming Language

    https://github.com/nopri/bakwan  
    Started by: Noprianto <nop@noprianto.com>
;

var ui_button_opt = {
    0: [-16750849, -1],
    1: [-16724992, -1],
    2: [-52429, -1],
    3: [-26368, -16777216],
    4: [-16777216, -1],
    5: [-1, -16777216]
}

var ui_c_btn_ = fn(a, t, m) {
    var b = component("button", a)
    var c = ui_button_opt[t]
    if (c != null) {
        config(b, "background", c[0])
        config(b, "foreground", c[1])
    }
    config(b, "active", m)
    return b
}

var ui_c_btn = fn(a) {
    return ui_c_btn_(a, 0, 0)
}


var ui_d_btn = fn(b, t) {
    var c = ui_button_opt[t]
    if (c != null) {
        config(b, "background", c[0])
        config(b, "foreground", c[1])
    }    
}

var ui_d_btn_s = fn(b) {
    config(b, "margin", [1, 2, 1, 2])
    return ui_d_btn(b, 5)
}

var ui_d_cmb_border = fn(c, b) {
    config(c, "border", b)
    config(c, "background", "white")
}

var ui_d_grid = fn(g) {
    config(g, "background", "white")
}

var ui_c_text = fn(a) {
    var g = component("grid", "text")
    var t = component("text", "")
    config(t, "border", a)
    var x = component("label", "")
    grid_add(g, t, 0, 0, 1, 1, 1, 1, 3, 1)
    grid_add(g, x, 0, 1, 1, 1, 0, 0, 0, 1)
    return [g, t, x]
}

var ui_s_text_label = fn(a, s, t) {
    if (!is(a, "ARRAY")) {
        return false
    }
    if (len(a) != 3) {
        return false
    }
    var g = a[0]
    if (!is(g, "COMPONENT")) {
        return false
    }
    var i = component_info(g)
    if (i[6] != "text") {
        return false
    }
    var x = a[2]
    var y = component_type(x)
    if (y != "label") {
        return false
    }
    var opt = {
        0: [-13421569, null, -1],
        1: [-52429, null, -52]
    }
    var c = opt[t]
    if (c != null) {
        config(x, "text", string(s))
        config(x, "foreground", c[0])
        config(a[1], "background", c[2])
    }
    return true    
}

var ui_c_disable_text = fn(c) {
    var lbl = component("label", "")
    config(c, "enabled", false)
    config(c, "background", get(lbl, "background"))
}

var ui_c_enable_text = fn(c) {
    config(c, "enabled", true)
    config(c, "background", "white")
}

var ui_set_font_size = fn(comp, size) {
    if ( (!is(comp, "COMPONENT")) | (!is(size, "NUMBER")) ) {
        return false
    }
    var comp_font = get(comp, "font")
    set(comp_font, 2, size)
    return config(comp, "font", comp_font)
}


