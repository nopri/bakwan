#
    about.singkong
    Information about the application/runtime
    part of Bakwan: simple accounting software
    
    Authors:
    Budiman (Budyzhang778@gmail.com)
    Noprianto (nop@noprianto.com)
;

var about_get_authors = fn(ctx) {
    var pad_a = 8 * " " 
    var pad_b = 12 * " "

    var a = pad_a + _("_developers")
    var b = []
    each(ctx["developers"], fn(e, i) {
        var role = ""
        if (!empty(e[0])) {
            var role = " [" + join(", ", e[0]) + "]"
        }
        var t = pad_b + e[1] + " (" + e[2] + ")" + role
        b + t
    })

    var c = pad_a + _("_contributors")
    var d = []
    each(ctx["contributors"], fn(e, i) {
        var role = ""
        if (!empty(e[0])) {
            var role = " [" + join(", ", e[0]) + "]"
        }
        var t = pad_b + e[1] + " (" + e[2] + ")" + role
        d + t
    })

    var n = newline()
    return 
        n + a + n + join(n, b) + n +
        n + c + n + join(n, d) 
}

var about_form = fn(ctx) {
    var about_grid = component("grid", "")
    var about_title = component("label", 
        _("title_about") + " " + ctx["name"], true)
    ui_set_font_size(about_title, 30)
    config(about_title, "align",  1)

    var pad_ = 8 * " "

    var tech_string = [
        [_("_config_file"), system_get_string(ctx["conf_path"])],
        [_("label_db_type"), system_get_string(ctx["db_type"])]
    ]
    if (ctx["db_type"] == system_db_type_user) {
        var tech_string = array_extend(tech_string, [
            [
                pad_ + _("label_user_db_path"), 
                userhome() + separator() + ctx["name"]
            ]
        ])
    }

    var tech_string = array_extend(tech_string, [
        [
            pad_ + _("label_db_host"), 
            system_get_string(ctx["db_host"])
        ],
        [
            pad_ + _("label_db_user"), 
            system_get_string(ctx["db_user"])
        ],
        [
            pad_ + _("label_db_name"), 
            system_get_string(ctx["db_name"])
        ]
    ])

    var general_about = component("edit", _("about_general_string"), true)
    var authors_about = component("edit", about_get_authors(ctx), true)
    var tech_about = component("table", _("table_about"), true)

    ui_set_font_size(general_about, 16)
    ui_set_font_size(authors_about, 16)
    config(tech_about, "contents", tech_string)
    grid_add(about_grid, about_title, 0, 0, 2, 1, 1, 0, 1, 0)
    grid_add(about_grid, general_about, 0, 1, 1, 1, 1, 1, 3, 0)
    grid_add(about_grid, authors_about, 0, 2, 1, 1, 1, 1, 3, 0)
    grid_add(about_grid, tech_about, 1, 1, 1, 2, 1, 1, 3, 0)

    custom_dialog(about_grid, _("title_about"), 
    ctx["dialog_width_2"], ctx["dialog_height_2"], 
    true, r)
}

